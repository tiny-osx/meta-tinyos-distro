require conf/distro/poky.conf

DISTRO = "poky-tinyos"
DISTROOVERRIDES = "poky:poky-tinyos"
DISTRO_NAME = "TinyOS Linux"
DISTRO_VERSION ?= "${@ '0.0.0-dev' if not d.getVar('SOURCECODE_VERSION') else d.getVar('SOURCECODE_VERSION') }"
DISTRO_CODENAME = "tinyos-kirkstone"
SDK_VENDOR = "-tinyossdk"
SDK_VERSION = "${@d.getVar('DISTRO_VERSION').replace('snapshot-${DATE}', 'snapshot')}"

# Set default timezone
DEFAULT_TIMEZONE = "America/Los_Angeles"

# Leave native enable to avoid build failures
ENABLE_WIDEC = "false"
ENABLE_WIDEC:class-native = "true"

# Drop native language support
USE_NLS="no"

# Enable sysvinit for init
INIT_MANAGER = "sysvinit"

# Enable distro feature options
DISTRO_FEATURES = "ipv4 ipv6"
DISTRO_FEATURES:class-native = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES}"
DISTRO_FEATURES:class-nativesdk = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES}"

DISTRO_FEATURES_BACKFILL_CONSIDERED:remove = "sysvinit"

# Using sysvinit without syslog daemon
VIRTUAL-RUNTIME_syslog = ""
VIRTUAL-RUNTIME_base-utils-syslog = ""

# The mtrace script included by eglibc is a perl script remove the dependency
RDEPENDS:${PN}-mtrace:pn-eglibc = ""

# Disable python usage in opkg-utils since it won't build with tinyOS config
PACKAGECONFIG:remove:pn-opkg-utils = "python"

# Set hostname
hostname:pn-base-files = "tinyos"